plugins {
    id 'java-library'
    id 'idea'
    id 'nu.studer.jooq'
    id "org.flywaydb.flyway"
}

sourceSets {
    main {
        java.srcDirs += "$buildDir/generated/sources/annotationProcessor/java"
    }

    funcTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

idea {
    module {
        testSourceDirs += sourceSets.funcTest.java.srcDirs
        testResourceDirs += sourceSets.funcTest.resources.srcDirs
    }
}

group 'com.marketplace'
version '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
}

jar {
    processResources {
        exclude("**/migration/*")
    }
}


configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }

    funcTestImplementation {
        extendsFrom testImplementation
    }
    funcTestRuntime {
        extendsFrom testRuntimeClasspath
    }

    funcTestRuntimeOnly {
        extendsFrom testRuntimeOnly
    }

    flywayMigration
}

dependencies {
    implementation project(":libs:config")
    implementation project(":libs:common")
    implementation project(":libs:cqrs-domain")
    implementation project(":eventstore")

    jooqGenerator "org.xerial:sqlite-jdbc:$sqliteVersion"
    jooqGenerator "org.jooq:jooq-meta-extensions:$jooqVersion"


    // 2.11.3
    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    implementation "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
//    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion"
    implementation "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion"

    implementation "org.flywaydb:flyway-core:$flywayVersion"
    // some functional implementations
    implementation("io.vavr:vavr:${vavrVersion}")

    implementation("org.apache.logging.log4j:log4j-slf4j-impl:2.9.0")

    implementation "org.xerial:sqlite-jdbc:$sqliteVersion"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    testImplementation("org.assertj:assertj-core:${assertjVersion}")
    testImplementation("org.mockito:mockito-core:${mockitoVersion}")
    testImplementation("org.mockito:mockito-junit-jupiter:${mockitoVersion}")

    funcTestImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    funcTestRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    funcTestImplementation("org.assertj:assertj-core:${assertjVersion}")
    funcTestImplementation("org.mockito:mockito-core:${mockitoVersion}")
    funcTestImplementation("org.mockito:mockito-junit-jupiter:${mockitoVersion}")

    funcTestImplementation project(":eventstore-tests")
    flywayMigration "org.xerial:sqlite-jdbc:$sqliteVersion"

    funcTestImplementation(testFixtures(project(":eventstore")))

}

test {
    useJUnitPlatform()
}

// https://flywaydb.org/documentation/usage/gradle/migrate
flyway {
    driver = 'org.sqlite.JDBC'
    url = "jdbc:sqlite:${projectDir}/src/main/resources/db/marketplace.db"
//    locations = ["classpath:migration"]
    locations = ["filesystem:${projectDir}/src/main/resources/db/migration"]
}

jooq {
    version = '3.14.7'  // the default (can be omitted)
    edition = nu.studer.gradle.jooq.JooqEdition.OSS  // the default (can be omitted)

    configurations {
        main {  // name of the jOOQ configuration
            generateSchemaSourceOnCompilation = true  // default (can be omitted)

            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'org.sqlite.JDBC'
                    url = 'jdbc:sqlite:src/main/resources/db/marketplace.db'
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.sqlite.SQLiteDatabase'
                    }
                    generate {
                        relations = true
                        deprecated = false
                        records = true
                        javaTimeTypes = true
                        immutablePojos = true
                        fluentSetters = true
                    }
                    target {
                        packageName = 'com.marketplace.eventstore.jdbc'
                        directory = 'src/main/jooq'  // default (can be omitted)
                    }
                    strategy.name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                }
            }
        }
    }
}

// after generating jooq configs, do not bring them here.
generateJooq {
    doLast {
        delete("src/main/resources/db/marketplace.db")
    }
}

generateJooq.dependsOn(flywayMigrate)

task funcTest(type: Test) {
    description = 'Runs the functional tests.'
    group = 'verification'
    testClassesDirs = sourceSets.funcTest.output.classesDirs
    classpath = sourceSets.funcTest.runtimeClasspath
    outputs.upToDateWhen { false }
    mustRunAfter test
}

funcTest {
    useJUnitPlatform()
}

check.dependsOn funcTest

clean {
    doLast {
        println "deleting marketplace.db"
        delete("src/main/resources/db/marketplace.db")
    }
}